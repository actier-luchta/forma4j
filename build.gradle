buildscript {
    ext {
        encoding = 'UTF-8'
        compatibility = 11
    }
}

plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'signing'
    id 'maven'
    id 'maven-publish'
    id 'antlr'
}

group = 'io.luchta'
version = '0.1.1'

sourceCompatibility = compatibility
targetCompatibility = compatibility

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'src-gen/main/java']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
    }
}

dependencies {
    implementation group: 'org.apache.poi', name: 'poi', version: '4.1.2'
    implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '4.1.2'

    // https://javaee.github.io/jaxb-v2/doc/user-guide/index.html
    implementation "javax.xml.bind:jaxb-api:2.3.1"
    implementation "org.glassfish.jaxb:jaxb-runtime:2.3.3"

    // https://mvnrepository.com/artifact/ognl/ognl
    implementation 'ognl:ognl:3.2.16'
    
	implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.12.2'
	implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.12.2'

    antlr "org.antlr:antlr4:4.9.1"

    testImplementation(platform('org.junit:junit-bom:5.7.0'))
    testImplementation('org.junit.jupiter:junit-jupiter')
}

test {
    useJUnitPlatform()
}

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'forma4j'
            from components.java
            pom {
                name = 'forma4j'
                description = 'Excelの読み書きを行うライブラリ'
                url = 'https://github.com/actier-luchta/forma4j'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'Actier,inc'
                    }
                }
                scm {
                    connection = 'git@github.com:actier-luchta/forma4j.git'
                    developerConnection = 'git@github.com:actier-luchta/forma4j.git'
                    url = 'https://github.com/actier-luchta/forma4j'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = hasProperty('sonatypeUsername') ? "${sonatypeUsername}" : ""
                password = hasProperty('sonatypeUsername') ? "${sonatypePassword}" : ""
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

generateGrammarSource {
    outputDirectory = file("src-gen/main/java")
}
